// BSLLS:CommonModuleInvalidType-off - тип модуля верен, может вызываться только в серверном контексте
// 
// Интерфейс хранения настроек сервисов
//  

#Область ПрограммныйИнтерфейс

// Возвращает настройки сервиса
//
// Параметры: 
// 	Сервис - Строка - Идентификатор сервиса
//
// Возвращаемое значение: 
// 	Соответствие, Неопределено
//
Функция Прочитать(Сервис) Экспорт
	
	Возврат Адаптер(Сервис).Прочитать(Сервис);
	
КонецФункции // Прочитать 

// Сохраняет настройки сервиса
//
// Параметры: 
// 	Сервис - Строка - Идентификатор сервиса
// 	Настройка - Соответствие - Сохраняемая настройка
//
Процедура Сохранить(Сервис, Настройка) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		Адаптер(Сервис).Сохранить(Сервис, Настройка);
		// И добавляем регионы использования адаптеров
		оас_СправкаАдаптер.РегионДобавить(Сервис);
		РегионДобавить(Сервис);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры // Сохранить 

// Добавляет регион использования хелперов
//
// Параметры: 
// 	Сервис - Строка - Идентификатор сервиса
//
Процедура РегионДобавить(Сервис) Экспорт
	
	оас_Подсистема.АдаптерВидРегионДобавить(Вид(), Сервис);
	
КонецПроцедуры // РегионДобавить 

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Инициализация вида адаптеров хранения настроек сервисов
//
// Параметры: 
//
Процедура Инициализировать() Экспорт
	
	оас_Модуль.ом_Подсистема().АдаптерИнициализировать(Вид()
	, Наименование()
	, Метаданные.Обработки.оас_НастройкиХранилищеСистемныхНастроек.Имя);
	
КонецПроцедуры // Инициализировать 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает объект адаптера
//
// Параметры: 
// 	Сервис - Строка - Идентификатор сервиса
//
// Возвращаемое значение: 
// 	ОбработкаОбъект
//
Функция Адаптер(Сервис)
	
	Возврат оас_Модуль.ом_АдаптерАктивный().Объект(Вид(), Сервис);
	
КонецФункции // Адаптер 

// Возвращает вид адаптеров харнения настроек
//
// Параметры: 
//
// Возвращаемое значение: 
// 	Строка
//
Функция Вид()
	
	Возврат "d83f7e9c-ecef-4a64-af04-8ddc0b7f44e7";
	
КонецФункции // Вид 

// Возвращает имя вида адаптеров хранения настроек сервисов
//
// Параметры: 
//
// Возвращаемое значение: 
// 	Строка
//
Функция Наименование()
	
	Возврат "ОАС. Хранение настроек";
	
КонецФункции // Наименование 

#КонецОбласти

// BSLLS:CommonModuleInvalidType-on
