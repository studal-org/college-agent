// BSLLS:CommonModuleInvalidType-off - тип модуля верен, может вызываться только в серверном контексте
// 
// Тело запроса/ответа по спецификации
// Тип тела определяется по набору узла "content" следующим образом
// 	Считается двоичными данными, если
// 		- хотя бы один элемент имеет оас_Тип.ДвоичныеДанныеЭто == Истина
// 		- хотя бы один элемент имеет mime тип двоичных данных
//  

#Область ПрограммныйИнтерфейс

// Создает внутреннее описание тела запроса/ответа
//
// Параметры: 
// 	Правила - Соответствие - Правила спецификации
//
// Возвращаемое значение: 
// 	Соответствие
//
Функция Создать(Правила) Экспорт
	
	Результат	= Новый Соответствие();
	
	ДвоичныеДанныеЭто	= Ложь;
	Обязательный		= Ложь;
	
	Тело	= Правила.Получить("requestBody");
	Если Тело <> Неопределено Тогда
		Для Каждого Элемент Из Тело["content"] Цикл
			Mime				= Элемент.Ключ;
			Тип					= Элемент.Значение;
			ТипЕсть				= оас_Тип.Определен(Тип);
			ДвоичныеДанныеЭто	= (ТипЕсть И оас_Тип.ДвоичныеДанныеЭто(Тип))
			ИЛИ (НЕ ТипЕсть И оас_Тип.MimeДвоичныеДанныеЭто(Mime));
			Если ДвоичныеДанныеЭто Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Булево			= оас_Модуль.ом_ОписаниеТипов().Создать("Булево");
		Обязательный	= Булево.ПривестиЗначение(Тело.Получить("required"));
	КонецЕсли;
	
	Результат.Вставить(ДвоичныеДанныеЭтоИмя(), ДвоичныеДанныеЭто);
	Результат.Вставить(ОбязательныйИмя(), Обязательный);
	
	Возврат Новый ФиксированноеСоответствие(Результат);
	
КонецФункции // Создать 

// Истина, если тело двоичные данные
//
// Параметры: 
// 	Тело - Соответствие - Описание тела запроса
//
// Возвращаемое значение: 
// 	Булево
//
Функция ДвоичныеДанныеЭто(Тело) Экспорт
	
	Возврат Тело[ДвоичныеДанныеЭтоИмя()];
	
КонецФункции // ДвоичныеДанныеЭто 

// Истина, если тело обязательно
//
// Параметры: 
// 	Тело - Соответствие - Описание
//
// Возвращаемое значение: 
// 	Булево
//
Функция Обязательный(Тело) Экспорт
	
	Возврат Тело[ОбязательныйИмя()];
	
КонецФункции // Обязательный 

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает имя свойства ДвоичныеДанныеЭто
//
// Параметры: 
//
// Возвращаемое значение: 
// 	Строка
//
Функция ДвоичныеДанныеЭтоИмя()
	
	Возврат "ДвоичныеДанныеЭто";
	
КонецФункции // ДвоичныеДанныеЭтоИмя 

// Возвращает имя свойства Обязательный
//
// Параметры: 
//
// Возвращаемое значение: 
// 	Строка
//
Функция ОбязательныйИмя()
	
	Возврат "Обязательный";
	
КонецФункции // ОбязательныйИмя 

#КонецОбласти

// BSLLS:CommonModuleInvalidType-on
