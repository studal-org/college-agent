// @strict-types
#Область ПрограммныйИнтерфейс
// Получить.
// 
// Параметры:
//  УИД - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  - Неопределено
//  - СправочникСсылка.ФизическиеЛица
Функция Получить(УИД) Экспорт
	ФизическоеЛицо = Справочники.ФизическиеЛица.ПолучитьСсылку(УИД);
	Если ФизическоеЛицо.ПолучитьОбъект() = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат ФизическоеЛицо;
КонецФункции

// Получить по электронной почте.
// 
// Параметры:
//  ЭлектроннаяПочта - Строка
// 
// Возвращаемое значение:
//  - Неопределено
//  - СправочникСсылка.ФизическиеЛица
Функция ПолучитьПоЭлектроннойПочте(ЭлектроннаяПочта) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК ФизическоеЛицо
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|ГДЕ
	|	ФизическиеЛицаКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.Email)
	|	И ФизическиеЛицаКонтактнаяИнформация.АдресЭП = &ЭлектроннаяПочта";

	Запрос.УстановитьПараметр("ЭлектроннаяПочта", ЭлектроннаяПочта);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	//@skip-check property-return-type
	Возврат Выборка.ФизическоеЛицо;
КонецФункции
#КонецОбласти