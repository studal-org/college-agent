// @strict-types
#Область ПрограммныйИнтерфейс
// Получить.
// 
// Параметры:
//  УИД - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  - Неопределено
//  - СправочникСсылка.УчебныеГруппы
Функция Получить(УИД) Экспорт
	УчебнаяГруппа = Справочники.УчебныеГруппы.ПолучитьСсылку(УИД);

	Если УчебнаяГруппа.ПолучитьОбъект() = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат УчебнаяГруппа;
КонецФункции

// Перечислить.
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.УчебныеГруппы
Функция Перечислить() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчебныеГруппы.Ссылка
	|ИЗ
	|	Справочник.УчебныеГруппы КАК УчебныеГруппы
	|ГДЕ
	|	УчебныеГруппы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУчебныхГрупп.Учится)";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Результат = Новый Массив; // Массив из см. СправочникСсылка.УчебныеГруппы

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		//@skip-check property-return-type
		Ссылка = ВыборкаДетальныеЗаписи.Ссылка; //см. СправочникСсылка.УчебныеГруппы
		Результат.Добавить(Ссылка);
	КонецЦикла;

	Возврат Результат;
КонецФункции

// Получить по студенту.
// 
// Параметры:
//  Студент - СправочникСсылка.Студенты
// 
// Возвращаемое значение:
//  - Неопределено
//  - СправочникСсылка.УчебныеГруппы
Функция ПолучитьПоСтуденту(Студент) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДвижениеКонтингентаСрезПоследних.Группа
	|ИЗ
	|	РегистрСведений.ДвижениеКонтингента.СрезПоследних КАК ДвижениеКонтингентаСрезПоследних
	|ГДЕ
	|	ДвижениеКонтингентаСрезПоследних.Студент = &Студент";

	Запрос.УстановитьПараметр("Студент", Студент);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	//@skip-check property-return-type
	Возврат Выборка.Группа;
КонецФункции
#КонецОбласти