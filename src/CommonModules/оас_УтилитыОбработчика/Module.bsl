// @strict-types

#Область ПрограммныйИнтерфейс

// Результат запроса в массив структур.
// 
// Параметры:
//  РезультатЗапроса - РезультатЗапроса
// 
// Возвращаемое значение:
//  Массив из Структура
Функция РезультатЗапросаВМассивСтруктур(РезультатЗапроса) Экспорт
	Результат = Новый Массив; // см. РезультатЗапросаВМассивСтруктур
	
	Выборка = РезультатЗапроса.Выбрать();
	Колонки = РезультатЗапроса.Колонки;
	Пока Выборка.Следующий() Цикл
		Элемент = Новый Структура;
		Для Каждого Колонка Из Колонки Цикл
			Ключ = Колонка.Имя;
			Значение = Выборка[Колонка.Имя]; // Произвольный
			Если ТипЗнч(Значение) = Тип("РезультатЗапроса") Тогда
				Элемент.Вставить(Ключ, РезультатЗапросаВМассивСтруктур(Значение));
			Иначе
				Элемент.Вставить(Колонка.Имя, Выборка[Колонка.Имя]);
			КонецЕсли;
		КонецЦикла;
		Результат.Добавить(Элемент);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти