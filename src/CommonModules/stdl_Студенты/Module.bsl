// @strict-types
#Область ПрограммныйИнтерфейс
// Получить студента.
// 
// Параметры:
//  УИД - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  - СправочникСсылка.Студенты
//  - Неопределено
Функция Получить(УИД) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДвижениеКонтингентаСрезПоследних.Студент КАК Студент
	|ИЗ
	|	РегистрСведений.ДвижениеКонтингента.СрезПоследних КАК ДвижениеКонтингентаСрезПоследних
	|ГДЕ
	|	ДвижениеКонтингентаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСтудентов.Студент)
	|	И УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ДвижениеКонтингентаСрезПоследних.Студент) = &УИД";

	Запрос.УстановитьПараметр("УИД", УИД);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	//@skip-check property-return-type
	Возврат Выборка.Студент;
КонецФункции

// Получить студента по физическому лицу.
// 
// Параметры:
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
// 
// Возвращаемое значение:
//  - СправочникСсылка.Студенты
//  - Неопределено
Функция ПолучитьПоФизическомуЛицу(ФизическоеЛицо) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДвижениеКонтингентаСрезПоследних.Студент КАК Студент
	|ИЗ
	|	РегистрСведений.ДвижениеКонтингента.СрезПоследних КАК ДвижениеКонтингентаСрезПоследних
	|ГДЕ
	|	ДвижениеКонтингентаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСтудентов.Студент)
	|	И ДвижениеКонтингентаСрезПоследних.Студент.Ссылка.ФизЛицо = &ФизическоеЛицо";

	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	//@skip-check property-return-type
	Возврат Выборка.Студент;
КонецФункции
#КонецОбласти